plugins {
    // https://maven.architectury.dev/architectury-plugin/architectury-plugin.gradle.plugin/
    id "architectury-plugin" version "3.4+"
    // https://maven.architectury.dev/dev/architectury/loom/dev.architectury.loom.gradle.plugin/
    id "dev.architectury.loom" version "1.3+" apply false
}

apply from: 'modprops.gradle'
version = project.mod_version
group = project.maven_group

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            // Via tags search on MC version at https://github.com/ParchmentMC/Parchment
            parchment("org.parchmentmc.data:parchment-1.18.2:2022.11.06@zip")
        }
    }
    
    clean.doLast {
        delete ".gradle"
        delete "build"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    // CosmicDan: Removed here and put into each individual subproject
    //archivesBaseName = 
    //version = 
    //group = 

    repositories {
        maven { url = "https://cursemaven.com" }
        maven { url = "https://jitpack.io" }
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
